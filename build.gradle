import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id "org.sonarqube" version "2.8"
    id "org.owasp.dependencycheck" version "6.0.1"
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java-library'
apply plugin: 'jacoco'

sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

def externalFileName = "$projectDir/.external"
if (file(externalFileName).exists()) {
    apply from: "external.gradle"
} else {
    apply from: "monext.gradle"
}



configurations {
    fatJarTest
}

ext {
    paymentMethodApiVersion = '1.9'
    powerMockVersion = '1.7.0'
    //paymentMethodIntegrationVersion = '1.4'
}

dependencies {
    compile group:'com.payline', name: 'payment-method-api', version: "$paymentMethodApiVersion"
    //testImplementation group: 'com.payline', name: 'payment-method-integration', version: "$paymentMethodIntegrationVersion"

    testImplementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.13.3'
    compileOnly group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.13.3'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.5'

    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.3.0'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.5.0'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.5.0'

}

// To create a JAR including test classes and dependencies
//task testJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
//    dependsOn 'shadowJar', 'testClasses'
//    classifier = 'tests'
//    from sourceSets.test.output.classesDirs + shadowJar
//    configurations = [project.configurations.fatJarTest]
//}

shadowJar {
    classifier = ''
    dependencies {
        exclude(dependency('com.payline:payment-method-api'))
        exclude(dependency('org.apache.logging.log4j:log4j-api'))
        exclude(dependency('org.apache.logging.log4j:log4j-core'))
        exclude(dependency('org.junit.jupiter:junit-jupiter-params'))
        exclude(dependency('org.junit.jupiter:junit-jupiter-api'))
        exclude(dependency('org.powermock:powermock-api-mockito2'))
    }
}

assemble.dependsOn shadowJar

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Built-By': System.getProperty('user.name'),
                'Built-JDK': System.getProperty('java.version'),
                'Build-Date': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Source-Compatibility': project.sourceCompatibility,
                'Target-Compatibility': project.targetCompatibility
        )
    }
}

processResources {
    // Replace tokens in release.properties file by their real value
    filesMatching('release.properties') {
        filter(ReplaceTokens, tokens: [
                'version': project.version.replaceFirst("-.*", ""),
                'date'   : new Date().format("dd/MM/yyyy")
        ])
    }
}

test {
    useJUnitPlatform()
    exclude('**/*IT.class')
}
jacocoTestReport {
    reports {
        xml.enabled true
    }
}